buttons = [1g, 2g, 3g, 4g, 5g, 6g, 7g, 8g, 9g, 10g, 11g, 13g, 14g, 15g, 16g, 17g]
selected = counter(0)
selector = 12g

gamescene = import gamescene

main = counter(1i)
sec = counter(2i)


on(gamescene.button_b(), {
    selected += 1
    if selected == buttons.length {
        selected -= buttons.length
        wait()
    }
    selector.move_to(buttons[selected.to_const(..buttons.length)])
})

current_op = counter()

sec_to_main = { sec.add_to([main], speed = 10) }

on(gamescene.button_a(), {
    if selected <= 9 {
        //number
        main.multiply(10, speed = 10)
        main.add(selected.to_const(0..10))
    } else if selected == 10 {
        main.reset(speed = 10)
    } else if selected == 11 {
        //solve
        if current_op == 0 {
            //plus
            sec_to_main!
        } else if current_op == 1 {
            //minus
            main.subtract_from([sec], speed = 10)
            sec_to_main!
        } else if current_op == 2 {
            //mult
            main.multiply(sec, speed = 7)
            sec.reset(speed = 10)
        } else if current_op == 3 {
            //div
            sec.divide(main, speed = 7)
            sec_to_main!
        }
    } else {
        //operator
        current_op = selected.to_const(12..17) - 12
        main.add_to([sec], speed = 10)
    }
})