#[no_std, cache_output]
extract import "constants.spwn"
impl @object {
	set: (self, key: @object_key, value) -> @NULL {
		$.edit_obj(self, key, value);
	},
	with: (self, key: @object_key, value) -> @object {
		let new_obj = self
		new_obj.set(key, value)
		return new_obj
	},
	has_key: (self, key: @object_key) -> @bool {
		return key in self
	},
	add_groups: (self, groups: @group | [@group]) -> @NULL {
		let grps = match groups {
			@group: [groups],
			[@group]: groups
		};

		if obj_props.GROUPS in self {

			grps += match self[obj_props.GROUPS] {
				@group: [self[obj_props.GROUPS]],
				[@group]: self[obj_props.GROUPS]
			};
		}
		self.set(obj_props.GROUPS, grps);
	},
	add: (self) -> @NULL {
		$.add(self);
	},
}
