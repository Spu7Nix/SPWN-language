#[no_std, cache_output]

extract import "control_flow.spwn"
extract import "constants.spwn".comparisons
import "events.spwn"
DEFAULT_BITS = 16

#[desc(u"
`counter` is a wrapper around the item ID system in Geometry Dash. In practice, it works as a replacement for numbers.
The difference between numbers and counters, is that counters can be modified in the level's runtime. 
")]
type @counter

reset_counter = (
    c, for_each: @number -> @NULL = (n){}
){

    for i in c.bits..0 {
        x = 2^i
        c.item.if_is(LARGER_THAN, x - 1, !{
            c.item.add(-x)
            for_each(x)
        })   
    }
}


impl @counter {

    new: #[constructor, desc("Creates a new counter"), example(u"
        @counter::new()     // creates a new counter with a starting value of 0
        @counter::new(10)   // creates a new counter with a starting value of 10
        @counter::new(5i)   // creates a new counter that uses item ID 5
        @counter::new(true)   // creates a new counter with a starting value of true (1)
    ")] (
        #[desc("Source (can be a number, item ID or boolean)")] source: @number | @item | @counter | @bool = 0,
        #[desc(
            "Defines the maximum stable size of the counter. If the counter goes outside of the range from 0 to 2^size, it's behavior will be undefined. Smaller sizes are more group effective."
        )] bits: @number = DEFAULT_BITS,
        #[desc("Resets the counter in case the item has been used before. This only applies if the `source` argument is not an item.")] reset: @bool = true,
    ) -> @counter {
        return match source {
            @number: (){
                id = ?i
                if source != 0 { id.add(source) }
                out = @counter::{
                    item: id,
                    bits,
                }
                if reset && $.trigger_fn_context() != 0g {
                    reset_counter(out) // in case the item has been used before
                }
                return out
            }(),
            @item: @counter::{
                item: source,
                bits,
            },
            @bool: (){
                if bits != DEFAULT_BITS && bits != 1 {
                    throw "A boolean counter can only have size = 1"
                }
                id = ?i
                
                if source {
                    id.add(1)
                }
                out = @counter::{
                    item: id,
                    bits: 1,
                }
                if reset && $.trigger_fn_context() != 0g {
                    reset_counter(out) // in case the item has been used before
                }
                return out
            }(),
            @counter: (){
                return source.clone()
            }(),
            else: @counter::{ item: ?i, bits: bits, },
        }
    }
}
impl @counter {
    display:
    #[desc("Creates a item display object that displays the value of the counter"), example(u"
        points = counter(0)
        points.display(75, 75)
    ")]
    (
        self,
        #[desc("X pos of display in units (1 grid square = 30 units)")] x: @number,
        #[desc("Y pos of display in units")] y: @number
    ) -> @NULL {
        extract import "constants.spwn".obj_props
        $.add(obj {
            OBJ_ID: 1615,
            X: x,
            Y: y,
            ITEM: self.item,
            COLOR: 1c,
            GROUPS: 999g
        });
    },

    
    to_obj:
    #[desc("Creates a item display object that displays the value of the counter"), example("
points = counter(0)
$.add( points.to_obj().with(obj_props.GROUPS, 5g) )
    ")]
    (
        self,
    ) -> @object {
        extract import "constants.spwn".obj_props
        return obj {
            OBJ_ID: 1615,
            ITEM: self.item,
            COLOR: 1c,
            GROUPS: 999g
        };
    },

    add_to:
    #[desc("Adds the counter's value to a counter (or all counters in a list), and resets the counter to 0 in the process"), example(u"
        a = counter(100)
        b = counter(0)
        wait(1)
        a.add_to(b)
        // a is now 0, b is now 100
    ")]
    (
        self,
        #[desc("Counter(s) to add to")] items: [@counter | @item] | @counter | @item,
        #[desc("Multiplier for the value added")] factor: @number = 1,
        #[desc("Macro to be called for each decrease of the counter. Takes one argument representing the number the counter is being decreased by")] for_each: @number -> @NULL = (n){}
    ) -> @NULL  {
        for i in self.bits..0 {
            x = 2^i

            -> if self >= x {
                self -= x
                if  items.type == @array {
                    for t in items {
                        t.add(x * factor)
                    }
                } else {
                    items.add(x * factor)
                }
                for_each(x)
            }
        }
    },

    add_to_multifactor: 
    #[desc("Like normal add_to, but each counter has its own factor")] (
        self,
        #[desc("Counter(s) to add to")] items: [[@counter | @number]],
    ) -> @NULL  {
        for i in self.bits..0 {
            x = 2^i

            -> if self >= x {
                self -= x
                
                for t in items {
                    t[0].add(x * t[1])
                }
                
            }
        }
    },

    subtract_from:
    #[desc("Subtracts the counter's value from another counter (or all counters in a list), and resets the counter to 0 in the process"), example(u"
        a = counter(100)
        b = counter(70)
        wait(1)
        b.subtract_from(a)
        // a is now 30, b is now 0
    ")]
    (
        self,
        #[desc("Counter(s) to subtract from")] items: [@counter | @item] | @counter | @item,
        #[desc("Multiplier for the value subtracted")] factor: @number = 1,
        #[desc("Macro to be called for each decrease of the counter. Takes one argument representing the number the counter is being decreased by")] for_each: @number -> @NULL = (n){}
    ) -> @NULL {
        for i in self.bits..0 {
            x = 2^i

            -> if self >= x {
                self -= x
                if  items.type == @array {
                    for t in items {
                        t.add(-x * factor)
                    }
                } else {
                    items.add(-x * factor)
                }
                for_each(x)
            }
        }
    },

    multiply:
    #[desc("Multiplies the value of the counter by some factor (does not consume the factor)"), example(u"
        c = counter(5)
        wait(1)
        c.multiply(10)
        // c is now 50
    ")]
    (
        self,
        #[desc("Factor to multiply by, either another counter (very expensive) or a normal number")] factor: @counter | @number,
    ) -> @NULL {

        if factor.type == @number {
            temp = @counter::new(0, reset = false, bits = self.bits)
            self.add_to([temp.item], factor)
            temp.add_to([self.item])
        } else if factor.type == @counter {
            result = @counter::new(0, reset = false, bits = self.bits)
            for i in self.bits..0 {
                x = 2^i
                
                -> if self >= x {
                    self -= x
                    factor.copy_to(result, factor = x)
                }     
            }
            result.add_to(self)

        }
    },


    divide: #[desc("Devides the value of the counter by some divisor"), example(u"
        c = counter(7)
        r = counter(0)
        wait(1)

        c.divide(2, remainder = r)
        // c is now 3, r is now 1
    ")]
    (
        self,
        #[desc("Divisor to divide by, either another counter (very expensive) or a normal number")] divisor: @counter | @number,
        #[desc("Counter or item to set to the remainder value")] remainder: @counter | @item = @counter::new(),
    ) -> @NULL {


        if divisor.type == @number {
            result = @counter::new(0, reset = false, bits = self.bits)
            for i in self.bits..0 {
                num = 2^i
                -> if self >= divisor * num {
                    self.add(-divisor * num)
                    result.add(num)
                }
            }
            self.add_to(remainder)
            result.add_to(self)
        } else if divisor.type == @counter {
            result = @counter::new(0, reset = false, bits = self.bits)
            broken = @counter::new(false)
            
            for i in self.bits..0 {
                num = 2^i

                divisor.copy_to(self, factor = -num)
                result.add(num)

                -> if self < 0 {
                    divisor.copy_to(self, factor = num)
                    result.add(-num)
                }
            }

            self.add_to(remainder)
            result.add_to(self)
        }
    },

    //will consume both numbers
    compare: #[desc("Returns 0 if both counters are equal, 1 if the other is smaller, and -1 if the other is greater."), example(u"
        c1 = counter(10)
        c2 = counter(15)

        cmp = c1.compare(c2) // -1
        // c1 is now -5, c2 is now 0
    ")](self, other: @counter, factor: @number = 1) -> in [-1, 0, 1] {
        comp = @counter::new(0, reset = false, bits = $.max(self.bits, other.bits))
        self.copy_to(comp)
        other.copy_to(comp, factor = -1)

        comp.item.if_is(EQUAL_TO, 0, !{
            -> return 0
        })
        comp.item.if_is(LARGER_THAN, 0, !{
            comp.reset()
            -> return 1
        }) 
        comp.item.if_is(SMALLER_THAN, 0, !{
            comp.reset_negative()
            -> return -1
        })

    },

    reset: #[desc("Resets counter to 0."), example(u"
        c = counter(100)
        wait(1)
        c.reset()
        // c is now 0
    ")]
    (
        self,
        #[desc("Macro to be called for each decrease of the counter. Takes one argument representing the number the counter is being decreased by")] for_each: @number -> @NULL = (n){}
    ) -> @NULL {

        reset_counter(self, for_each)
    },

    reset_negative: #[desc("Resets a negative counter to 0"), example(u"
        c = counter(-100)
        wait(1)
        c.reset_negative()
        // c is now 0
    ")]
    (
        self,
        #[desc("Macro to be called for each decrease of the counter. Takes one argument representing the number the counter is being decreased by")] for_each: @number -> @NULL = (n){}
    ) -> @NULL {

        for i in self.bits..0 {
            x = 2^i
            -> if self <= -x {
                self += x
                for_each(x)
            }     
        }
    },

    copy_to: #[desc("Copies the value of the counter to another counter (or to all counters in a list), without consuming the original"), example(u"
        c1 = counter(100)
        c2 = counter(0)
        wait(1)
        c1.copy_to(c2)
        // both counters are now 100
    ")]
    (
        self,
        #[desc("Counter(s) to copy to")] items: [@counter | @item] | @counter | @item,
        #[desc("Factor of to multiply the copy by")] factor: @number = 1
    ) -> @NULL {
        temp_storage = @counter::new(0, reset = false, bits = self.bits)

        for i in self.bits..0 {
            x = 2^i

            -> if self >= x {
                self -= x
                temp_storage += x
                if  items.type == @array {
                    for t in items {
                        t.add(x * factor)
                    }
                } else {
                    items.add(x * factor)
                }
            }
        }
        temp_storage.add_to(self)

    },

    clone: #[desc("Copies the counter and returns the copy"), example(u"
        c1 = counter(100)
        c2 = c1.clone()
        // c1 and c2 are now 100
    ")] (
        self
    ) -> @counter {
        new_counter = @counter::new(0, bits = self.bits)
        self.copy_to([new_counter])
        return new_counter
    },

    _plus_: #[desc("Implementation of the plus (`+`) operator"), example(u"
        c1 = counter(10)
        c2 = c1 + 10
        // c2 is 20
    ")]
    (self, other: @number | @counter) -> @counter {
        if other.type == @number {
            new_counter = self.clone()
            new_counter.add(other)
            -> return new_counter
        } else if other.type == @counter {
            new_counter = self.clone()
            other.copy_to([new_counter.item], factor = 1)
            -> return new_counter
        }
    },

    _minus_: #[desc("Implementation of the minus (`-`) operator"), example(u"
        c1 = counter(10)
        c2 = c1 - 3
        // c2 is 7
    ")]
    (self, other: @number | @counter) -> @counter {
        if other.type == @number {
            new_counter = self.clone()
            new_counter.add(-other)
            return new_counter
        } else if other.type == @counter {
            new_counter = self.clone()
            other.copy_to([new_counter.item], factor = -1)
            return new_counter
        }
    },

    _times_: #[desc("Implementation of the times (`*`) operator"), example(u"
        c1 = counter(10)
        c2 = c1 * 10
        // c2 is 100
    ")]
    (self, other: @number | @counter) -> @counter {
        if other.type == @number {
            new = @counter::new(0, bits = self.bits)
            self.copy_to(new, factor = other)
            return new
        } else if other.type == @counter {
            new = self.clone()
            new.multiply(other)
            return new
        }
        

    },

    _divided_by_: #[desc("Implementation of the divided by (`/`) operator"), example(u"
        c1 = counter(100)
        c2 = c1 / 10
        // c2 is 10
    ")]
    (self, num: @number | @counter) -> @counter {
        clone = self.clone()
        clone.divide(num)
        -> return clone
    },

    _mod_: #[desc("Implementation of the modulus (`%`) operator"), example(u"
        c1 = counter(42)
        c2 = c1 % 10
        // c2 is 2
    ")]
    (self, num: @number | @counter) -> @counter {
        clone = self.clone()
        out = @counter::new(0, bits = self.bits)
        clone.divide(num, remainder = out)
        -> return out
    },

    _modulate_: #[desc("Implementation of the modulate (`%=`) operator"), example(u"
        c = counter(42)
        c %= 10
        // c is 2
    ")]
    (self, num: @number | @counter) -> @NULL {
        out = @counter::new(0, bits = self.bits)
        self.divide(num, remainder = out)
        self.reset()
        out.add_to([self])
    },

    _more_than_: #[desc("Implementation of the more than (`>`) operator"), example(u"
        c = counter(42)
        more = c > 10
        // more is now true
    ")]
    (self, other: @number | @counter) -> @bool {

        if other.type == @number {

            ret_false = !{
                -> return false
            }

            self.item.if_is(LARGER_THAN, other, !{
                ret_false.start_group.toggle_off()
                -> return true
            })

            self.item.if_is(SMALLER_THAN, other + 1, !{ ret_false! })

            ret_false.start_group.toggle_on()
            
        } else if other.type == @counter {
            cmp = self.compare(other)
            return cmp == 1
        }
    },

    _less_than_: #[desc("Implementation of the less than (`<`) operator"), example(u"
        c = counter(42)
        less = c < 42
        // less is now false
    ")]
    (self, other: @number | @counter) -> @bool {

        if other.type == @number {

            ret_false = !{
                -> return false
            }

            self.item.if_is(SMALLER_THAN, other, !{
                ret_false.start_group.toggle_off()
                -> return true
            })

            self.item.if_is(LARGER_THAN, other - 1, !{ ret_false! })

            ret_false.start_group.toggle_on()
        } else if other.type == @counter {
            cmp = self.compare(other)
            -> return cmp == -1
        }
    },

    _more_or_equal_: #[desc("Implementation of the more than or equal (`>=`) operator"), example(u"
        c = counter(42)
        more_or_eq = c >= 10
        // more_or_eq is now true
    ")]
    (self, other: @number | @counter) -> @bool {

        if other.type == @number {

            ret_false = !{
                -> return false
            }

            self.item.if_is(LARGER_THAN, other - 1, !{
                ret_false.start_group.toggle_off()
                -> return true
            })

            self.item.if_is(SMALLER_THAN, other, !{ ret_false! })

            ret_false.start_group.toggle_on()
        } else if other.type == @counter {
            cmp = self.compare(other)
            -> return cmp == 1 || cmp == 0
        }
    },

    _less_or_equal_: #[desc("Implementation of the less than or equal (`<=`) operator"), example(u"
        c = counter(42)
        less_or_eq = c <= 42
        // less_or_eq is now true
    ")]
    (self, other: @number | @counter) -> @bool {

        if other.type == @number {

            ret_false = !{
                -> return false
            }

            self.item.if_is(SMALLER_THAN, other + 1, !{
                ret_false.start_group.toggle_off()
                -> return true
            })

            self.item.if_is(LARGER_THAN, other, !{ ret_false! })

            ret_false.start_group.toggle_on()
        } else if other.type == @counter {
            cmp = self.compare(other)
            -> return cmp == -1 || cmp == 0
        }
    },

    _equal_: #[desc("Implementation of the equals (`==`) operator"), example(u"
        c = counter(42)
        eq = c == 42
        // eq is now true
    ")]
    (self, other: @number | @counter) -> @bool {
        if other.type == @number {

            ret_false = !{
                -> return false
            }

            self.item.if_is(EQUAL_TO, other, !{
                ret_false.start_group.toggle_off()
                -> return true
            })

            activate_group_fix = !{ ret_false! }

            self.item.if_is(LARGER_THAN, other, activate_group_fix)
            self.item.if_is(SMALLER_THAN, other, activate_group_fix)

            ret_false.start_group.toggle_on()
        } else if other.type == @counter {
            cmp = self.compare(other)
            -> return cmp == 0
        }
    },

    _not_equal_: #[desc("Implementation of the not equal (`!=`) operator"), example(u"
        c = counter(42)
        not_eq = c != 42
        // not_eq is now false
    ")](self, other: @number | @counter) -> @bool {
        -> return !(self == other)
    },

    add: #[desc("Implementation of the pickup trigger"), example(u"
        c = counter(10)
        c.add(10)
        // c is now 20
    ")]
    (self, #[desc("Amount to add")] num: @number) -> @NULL {
        self.item.add(num)
    },

    _add_: #[desc("Implementation of the add (`+=`) operator"), example(u"
        c = counter(10)
        c += 10
        // c is now 20
    ")](self, num: @number | @counter) -> @NULL {
        if num.type == @number {

            self.add(num)
        } else if num.type == @counter {
            num.copy_to(self) //holy shit why was this not like this all along
        }
    },

    _increment_: #[desc("Implementation of the increment (`n++`) operator. Does not return any value."), example(u"
        c = counter(10)
        c++
        // c is now 11
    ")](self) -> @NULL {
        self.add(1)
    },

    _decrement_: #[desc("Implementation of the decrement (`n--`) operator. Does not return any value."), example(u"
        c = counter(10)
        c--
        // c is now 9
    ")](self) -> @NULL {
        self.add(-1)
    },

    _subtract_: #[desc("Implementation of the subtract (`-=`) operator"), example(u"
        c = counter(20)
        c -= 5
        // c is now 15
    ")](self, num: @number | @counter) -> @NULL {
        if num.type == @number {

            self.add(-num)

        } else if num.type == @counter {
            num.copy_to(self, factor = -1)
        }
    },

    _multiply_: #[desc("Implementation of the multiply (`*=`) operator"), example(u"
        c = counter(5)
        c *= 6
        // c is now 30
    ")](self, num: @number | @counter) -> @NULL {

        if num.type == @number {
            self.multiply(num)
        } else if num.type == @counter {
            self.multiply(num)
        }
    },

    _divide_: #[desc("Implementation of the divide (`/=`) operator"), example(u"
        c = counter(30)
        c /= 6
        // c is now 5
    ")](self, num: @number | @counter) -> @NULL {
        if num.type == @number {
            self.divide(num)
        } else if num.type == @counter {
            self.divide(num)
        }
    },

    _assign_: #[desc("Implementation of the assign (`=`) operator"), example(u"
        c = counter(23)
        c = 42
        // c is now 42
    ")](self, num: @number | @counter | @bool) -> @NULL {
        self.reset()
        if num.type == @number {
            if self.bits == 1 {
                throw "You can only assign booleans to a counter initialized with a boolean"
            }
            if num > 0 {
                self.add(num)
            }
        } else if num.type == @bool {
            if self.bits != 1 {
                throw "You can only assign a boolean to a counter initialized with a boolean (for example `counter(true)`)"
            }
            if num {
                self.add(1)
            }
        } else if num.type == @counter {
            num.copy_to(self)
        }
    },

    _swap_: #[desc("Implementation of the swap (`<=>`) operator"), example(u"
        c1 = counter(23)
        c2 = counter(42)
        wait(1)
        c1 <=> c2
        // c1 is now 42, c2 is now 23
    ")]
    (self, num: @counter) -> @NULL {
        swap_tmp = @counter::new(0, bits = self.bits, reset = false);
        self.add_to(swap_tmp)
        num.add_to(self)
        swap_tmp.add_to(num)
    },

    to_const: #[desc("Converts the counter into a normal number (very context-splitting, be careful)"), example(u"
        c = counter(3)
        wait(1)
        10g.move(c.to_const(0..10) * 10, 0, 1)
        // group ID 10 moves 3 blocks
    ")] (
        self,
        #[desc("Array or range of possible output values")] range: [@number] | @range
    ) -> @number {
        let funcs = []

        for val in range {
            funcs.push([val, !{
                -> return val
            }])
        }


        for val in range {
            -> self.item.if_is(EQUAL_TO, val, !{
                for func in funcs {
                    [val2, func] = func
                    if val2 != val {
                        func.start_group.toggle_off()
                    } else {
                        func!
                    }
                }
                
            })
        }

        for func in funcs {
            func[1].start_group.toggle_on()
        }
    },

    to_const_enclosed: #[desc("Converts the counter into a normal number that you can use within a macro"), example(u"
        c = counter(3)
        wait(1)
        c.to_const_enclosed(0..10, (c_const) {
            10g.move(c_const * 10, 0, 1)
        })
        // group ID 10 moves 3 blocks
    ")] (
        self,
        #[desc("Array or range of possible output values")] range: [@number] | @range,
        #[desc("Closure where you can use the const value, should take the value as the first argument")] closure: @number -> @NULL,

    ) -> @NULL {
        -> () {
            val = self.to_const(range)
            closure(val)
        } ()
    },

    _as_: #[desc("Implementation of the as (`as`) operator"), example(u"
        c = counter(true)
        b = c as @bool
        // b is now true
    ")]
    (self, _type: @type_indicator) -> _ {
        if _type == @bool {
            if self.bits != 1 {
                throw "A counter must be initialized with a boolean (for example `counter(true)`) for it to be convertable to a boolean"
            }
            -> return self != 0
        } else {
            throw "Cannot convert counter to " + _type as @string + " (counter can convert to a number using the counter.to_const macro)"
        }
    },

    reaches: #[
        desc("Returns an event for when the counter reaches a certain value"), example(u"
            c = counter(2)

            on(c.reaches(10), !{
                BG.pulse(rgb8(0, 255, 0), fade_out = 0.5) // will pulse each time the counter becomes 10
            })
        ")
    ] (self, #[desc("Value to reach")] value: @number) -> @event {
        return self.item.count(value)
    },

    _display_: #[desc("Formats the counter to a printable string")] (self) -> @string {
        return "counter(" + $.display(self.item) + ", bits = " + $.display(self.bits) + ")"
    },

}
