#[no_std, cache_output]

extract constants.obj_ids
extract constants.obj_props
extract import 'events.spwn'
extract import 'control_flow.spwn'
import "counter.spwn"

let frames_setup_cache = null

frames_setup = () {
    if frames_setup_cache != null { return frames_setup_cache }

    block_a = ?b
    block_b = ?b
    group_toggle = ?g
    group_first = ?g
    group_second = ?g
    group_frame = ?g

    frames_setup_cache = {
        block_a: block_a,
        block_b: block_b,
        group_toggle: group_toggle,
        group_first: group_first,
        group_second: group_second,
        group_frame: group_frame,
    }

    $.add(obj{
        OBJ_ID: special.COLLISION_BLOCK,
        X: 0,
        Y: 2400,
        BLOCK_A: block_a,
        GROUPS: group_toggle,
        DYNAMIC_BLOCK: true,
    })
    $.add(obj{
        OBJ_ID: special.COLLISION_BLOCK,
        X: 0,
        Y: 2400,
        BLOCK_A: block_b,
    })

    collision(block_a, block_b).on(!{
        group_toggle.toggle_off()
        group_first!
        group_frame!
    })
    collision_exit(block_a, block_b).on(!{
        group_toggle.toggle_on()
        group_second!
        group_frame!
    })

    call_on_x_position(1, !{
        group_toggle.toggle_off()
    })

    return frames_setup_cache
}

return {
    frames: #[desc("Waits for an arbitrary amount of frames"), example(u"
        @log::runtime::flash(rgb(1,0,0)) // flash the screen
        frames(60) // wait 60 frames (1 second on 60fps)
        @log::runtime::flash(rgb(0,0,1)) // flash the screen
    ")] (
        #[desc("The amount of frames to wait")] frames: @number|@counter,
    ) -> @NULL {
        setup = frames_setup()

        frames_count = @counter::new(bits = $.ceil($.log(frames, 2)))

        extern = !{ // currently doing this won't work, and it will throw an error from optimizer.rs
            if frames_count < frames {
                frames_count++
            } else if frames_count == frames {
                frames_count++
                -> return
            }
        }

        $.extend_trigger_func(setup.group_frame, (){
            extern!
        })
    },

    frame: #[desc("Returns an event that runs on every frame"), example(u"
        on(frame(), !{
            10g.move(1, 0) // moves every frame (on 60fps the block would move by 6 blocks per second)
        })
    ")] () -> @event {
        event = @event::new()
        $.extend_trigger_func(
            frames_setup().group_frame,
            () => event.emit(),
        )
        return event
    },
}
