RIGHT = 1b
LEFT = 2b
INCR = 3b
DECR = 4b
DOT = 5b
OPEN = 6b //opening bracket
CLOSE = 7b //closing bracket

spacing = 10

insert_bf_script = (str: @string, offset: [@number]) {
    let index = 0
    for ch in str {
        blockid = switch ch {
            ==">": RIGHT,//increment the data pointer (to point to the next cell to the right).
            =="<": LEFT,	//decrement the data pointer (to point to the next cell to the left).
            =="+": INCR,	//increment (increase by one) the byte at the data pointer.
            =="-": DECR,	//decrement (decrease by one) the byte at the data pointer.
            ==".": DOT	//output the byte at the data pointer.
            //==",": 	//accept one byte of input, storing its value in the byte at the data pointer.
            =="[": OPEN,	
            =="]": CLOSE,
            else: null 
        }
        if blockid != null {
            extract obj_props
            $.add(obj {
                OBJ_ID: 1816,
                X: offset[0] + index * spacing * 3,
                Y: offset[1],
                BLOCK_A: blockid,
                SCALING: 0.2,
            })
            $.add(obj {
                OBJ_ID: 914,
                X: offset[0] + index * spacing * 3,
                Y: offset[1] - 30,
                TEXT: $.b64encode(ch),
                COLOR: 1c,
            })
            index++
        }
    }
}

type @bytedisplay 

impl @bytedisplay {
    new: (offset: [@number]) {
        1c.set(255, 255, 255)
        //order = "!\"#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[ ]^_`abcdefghijklmnopqrstuvwxyz{|}~"
        order = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
        let list = []
        for ch in order {
            color = ?c
            extract obj_props
            $.add(obj {
                OBJ_ID: 914,
                X: offset[0],
                Y: offset[1],
                TEXT: $.b64encode(ch),
                COLOR: color,
            })
            color.set(0,0,0, blending = true)
            list.push(color)
        }
        return @bytedisplay::{list: list, chars: order.length}
    },
    display: (self, index: @number) {
        for i in ..self.list.length {
            if i == index {
                -> self.list[index].pulse(0,255,0, fade_out = 0.5)
                -> self.list[index].set(255,255,255, 0.5)
            } else {
                self.list[i].set(0,0,0, blending = true)
            }
        }
    }
}

type @bfreader
layers = counter(0)
layers.display(150, 300)

impl @bfreader {
    new: (script_offset: [@number], cell_count: @number) {
        
        extract obj_props
        block = ?b
        group = ?g
        $.add(obj {
            OBJ_ID: 1816,
            X: script_offset[0] - spacing * 6,
            Y: script_offset[1],
            GROUPS: group,
            BLOCK_A: block,
            SCALING: 0.5,
            DYNAMIC_BLOCK: true,
        })
        for b in [RIGHT,LEFT,INCR,DECR,DOT,OPEN,CLOSE] {
            $.add(obj {
                OBJ_ID: 1816,
                X: script_offset[0] - spacing * 3,
                Y: script_offset[1],
                SCALING: 0.5,
                BLOCK_A: b,
            })
        }
        

        // CREATE CELLS
        let cells = []
        for i in ..cell_count {
            c = counter()
            cells.push(c)
            c.display(script_offset[0] + i * 45, script_offset[1] + 90)
        }
        ptr = counter(0)
        ptr.display(script_offset[0], script_offset[1] + 60)
        

        std_out = @bytedisplay::new([script_offset[0], script_offset[1] + 120])
        out = @bfreader::{
            group: group,
            right: counter(block.create_tracker_item(RIGHT)),
            left:  counter(block.create_tracker_item(LEFT)),
            incr:  counter(block.create_tracker_item(INCR)),
            dot:  counter(block.create_tracker_item(DOT)),
            decr:  counter(block.create_tracker_item(DECR)),
            open:  counter(block.create_tracker_item(OPEN)),
            close: counter(block.create_tracker_item(CLOSE)),
            std_out: std_out,
            ptr: ptr,
            cells: cells,
        }
        group.move(spacing * 2, 0, 0.3)

        return out
    },
    currently_on: (self) {
        if self.right == 1 {
            return RIGHT
        } else if self.left == 1 {
            return LEFT
        } else if self.incr == 1 {
            return INCR
        } else if self.decr == 1 {
            return DECR
        } else if self.dot == 1 {
            return DOT
        } else if self.open == 1 {
            return OPEN
        } else if self.close == 1 {
            return CLOSE
        }
    },
    current_cell: (self) => self.cells[self.ptr.to_const(..self.cells.length)],
    interpret: (self) {
        
        ret = !{
            self.group.move(spacing, 0, 0)
            -> return
        }
        current = self.currently_on()
        
        if current == RIGHT {
            self.ptr += 1
            ret!
        }
        else if current == LEFT {
            self.ptr -= 1
            ret!
        }
        else if current == INCR {
            -> self.current_cell() += 1
            ret!
        }
        else if current == DECR {
            -> self.current_cell() -= 1
            ret!
        }
        else if current == DOT {
            std_out = counter(0)
            () {
                std_out.reset()
                cell = self.current_cell()
                wait()
                cell.copy_to([std_out])
            } ()

            self.std_out.display(std_out.to_const(..self.std_out.chars))
            ret!
        }
        else if current == OPEN {
            move_to = !{
                condition = () => !(layers == 0 && self.close == 1)
                
                while_loop(condition, delay = 0.05, (){
                    
                    
                    -> if self.open == 1{
                        layers += 1
                    }
                    
                    -> if self.close == 1 {
                        layers -= 1
                    }

                    self.group.move(spacing, 0) 
                })
                ret!
            }
            if self.current_cell() == 0 {
                layers -= 1
                call_with_delay(0.05, move_to)
            } else {
                ret!
            }
        }
        else if current == CLOSE {
            move_back = !{
                condition = ()=> !(layers == 0 && self.open == 1)
                while_loop(condition, delay = 0.05, (){
                    
                    -> if self.close == 1 {
                        layers += 1
                    }
                    
                    -> if self.open == 1 {
                        layers -= 1
                    }
                    
                    self.group.move(-spacing, 0)
                        
                })
                
                ret!
            }
            if self.current_cell() != 0 {
                layers -= 1
                call_with_delay(0.05, move_back)
            } else {
                ret!
            }
        }
        
        
    }
}

offset = [300, 300]
insert_bf_script(">>++++>+++[-<-[<]>>]<.", offset)

reader = @bfreader::new(offset, 100)
while_loop(()=>true, (){
    reader.interpret()
})